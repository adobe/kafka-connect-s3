apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

ext."signing.keyId" = '3D5954DE'
ext."signing.password" = System.env.KEY_PASS
ext."signing.secretKeyRingFile" = "${rootDir}/secring"

signing {	
	required { gradle.taskGraph.hasTask("uploadArchives") }
	sign configurations.archives
}

artifacts {
	archives(tasks.shadowJar) {
		classifier 'shadow'
		builtBy tasks.shadowJar
	}
}

def ossrhUsername = System.env.OSSRH_USER
def ossrhPassword = System.env.OSSRH_PASS

apply plugin: 'io.codearte.nexus-staging'

// not currently working... https://oss.sonatype.org/#stagingRepositories
nexusStaging {
	stagingProfileId = "28dfc7f49c610c"
	numberOfRetries = 20
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name pomHumanName
				packaging 'jar'
				// optionally artifactId can be defined here
				description pomDesc
				url 'https://github.com/spredfast/kafka-connect-s3'

				scm {
					connection 'scm:git:https://github.com/spredfast/kafka-connect-s3.git'
					developerConnection 'scm:git:https://github.com/spredfast/kafka-connect-s3.git'
					url 'https://github.com/spredfast/kafka-connect-s3'
				}

				licenses {
					license {
						name 'BSD License'
						url 'https://github.com/spredfast/kafka-connect-s3/blob/master/LICENSE.md'
					}
				}

				developers {
					developer {
						id 'iamnoah'
						name 'Noah Sloan'
						email 'nsloan@spredfast.com'
					}
				}
			}
		}
	}
}
uploadArchives.repositories.mavenDeployer.pom.whenConfigured { MavenPom pom ->
	pom.dependencies = []
}
